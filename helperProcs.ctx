--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: helperProcs
Element-Type: context
Element-Version: 2.0
Id: _2UF_4DbkEeWPP_kFBF0hLg
Runtime-Version: 2.0.0.201506120617
Save-Time: 8/5/15 2:00 PM

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa
Content-Type: text/plain
Entry-Name: .description

Helper procedures. Typically used by procedures in userProcs.ctx but may be called directly from test cases.
This should be set as a Default Context in the Project Settings.

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa--
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

// ----------------------------------------------------------------------------
// Description:
// This file contains helper procedures typically called by procedures in
// the userProcs.ctx file. This does not preclude the direct use of these
// procedures, but more care must be taken that the assumptions of these
// procedures have been satisfied.
// ----------------------------------------------------------------------------


// ----------------------------------------------------------------------------
// Checks that a Tree-Item is a System Implementation.
//
// Description:
// Given an Item object, checks that it is a System Implementation.
// This is not limited to a Tree-Item but the typical usage is for 
// iterating through a Tree in the Eclipse Outline view to find the
// System Implementations.
//
// Assumptions:
// - the given Item argument is from the Eclipse Outline view
//
// Arguments:
// item tree_item - An item (typically from a Tree) to check 
//                  as system implmentation
//
// Return:
// bool: true if system implementation, false otherwise
//
// Usage:
// get-view Outline | get-tree | get-items | foreach [val item] {
//    if [is-system-impl $item] {
//       ...do something...
//    }
// }
// ----------------------------------------------------------------------------
proc "is-system-impl" [val tree_item] {
    //$tree_item | get-text | contains ".Impl"
    $tree_item | get-text | contains "System Impl"
}


// ----------------------------------------------------------------------------
// Checks that AGREE is still running
//
// Description:
// Checks the Result (second) column of the AGREE Resuls view to see if AGREE
// is still running. Checks for any result that has the text "Waiting" or
// "Working" as an indicator that AGREE is running.
//
// Assumptions:
// - AGREE has been started by another procedure
//
// Return:
// bool true if running, false otherwise
// ----------------------------------------------------------------------------
proc "is-agree-working" {

    or
    [get-view "AGREE Results" | get-tree | get-items | foreach [val item] {
        if [$item | get-text | contains "Contract Guarantees"] { 
            $item | get-property "columns[1]" -raw | contains "Working" 
        }
    }]
    [get-view "AGREE Results" | get-tree | get-items | foreach [val item] {
        if [$item | get-text | contains "Contract Consistency"] { 
            $item | get-property "columns[1]" -raw | contains "Working" 
        }
    }]
    [get-view "AGREE Results" | get-tree | get-items | foreach [val item] {
        if [$item | get-text | contains "Contract Guarantees"] { 
            $item | get-property "columns[1]" -raw | contains "Waiting" 
        }
    }]
    [get-view "AGREE Results" | get-tree | get-items | foreach [val item] {
        if [$item | get-text | contains "Contract Consistency"] { 
            $item | get-property "columns[1]" -raw | contains "Waiting" 
        }
    }]

    // Only works for Single Layer
    //or[get-view "AGREE Results" | get-tree | get-item "Contract Guarantees" | get-property "columns[1]" -raw | contains "Working" ]
    //  [get-view "AGREE Results" | get-tree | get-item "Contract Guarantees" | get-property "columns[1]" -raw | contains "Waiting" ]
    //  [get-view "AGREE Results" | get-tree | get-item "Contract Consistency" | get-property "columns[1]" -raw | contains "Working" ]
    //  [get-view "AGREE Results" | get-tree | get-item "Contract Consistency" | get-property "columns[1]" -raw | contains "Waiting" ]
}


// ----------------------------------------------------------------------------
// Wait for AGREE to finish running
//
// Description:
// Runs a loop calling the procedure "is-agree-working", if AGREE is still running will
// wait and then check again. When AGREE has finished will return.
//
// Assumptions:
// - AGREE has started. Behavior undefined otherwise.
// ----------------------------------------------------------------------------
proc "wait-for-agree" {
    loop {
        if [is-agree-working | eq true] {
            wait 500 
            recur
        } -else {}
    }
}


// ----------------------------------------------------------------------------
// Given an Item will run AGREE Verify Single Layer
// 
// Description:
// Given an Item (presumably from the Outline view) will run the AGREE 
// Single Layer. Does allow for a status dialog pop-up if titled
// "Problem Occurred". 
//
// Assumptions:
// - The item is from the Outline View
// - The item is a system implementation, otherwise will follow exception path
//
// Notes:
// The try-catch does not work in ECL as expected. With the catch defined, the
// try will only execute the first statement meaning that the verify-false 
// will not stop the execution as intended. In typical usage another
// downstream failure will stop the execution.
//
// Arguments:
// item item - Typically Outline view item that is a system implementation
// ----------------------------------------------------------------------------
proc "run-agree-single-layer" [val item] {
    exec-with-options {
        $item | select-item | get-menu "AGREE/Verify Single Layer" | click
        try -command {			
            get-window "Problem Occurred" | get-button OK | click | verify-false
        } -catch {}
    } -allowStatusDialog
}


// ----------------------------------------------------------------------------
// Given an Item will run AGREE Verify All Layers
// 
// Description:
// Given an Item (presumably from the Outline view) will run the AGREE 
// All Layers. Does allow for a status dialog pop-up if titled
// "Problem Occurred". 
//
// Assumptions:
// - The item is from the Outline View
// - The item is a system implementation, otherwise will follow exception path
//
// Notes:
// The try-catch does not work in ECL as expected. With the catch defined, the
// try will only execute the first statement meaning that the verify-false 
// will not stop the execution as intended. In typical usage another
// downstream failure will stop the execution.
//
// Arguments:
// item item - Typically Outline view item that is a system implementation
// ----------------------------------------------------------------------------
proc "run-agree-all-layers" [val item] {
    exec-with-options {
        $item | select-item | get-menu "AGREE/Verify All Layers" | click
        try -command {
            get-window "Problem Occurred" | get-button OK | click | verify-false
            bool false | verify-true // cheap way to stop further execution
        } -catch {}
    } -allowStatusDialog
}


// ----------------------------------------------------------------------------
// Checks that the AGREE Contract Guarantees are valid.
//
// Desription:
// Checks the AGREE Results view and determines if the top-level Contract
// Guarantees reports valid. Will wait for AGREE to finish before checking.
// 
// Assumptions:
// - Only works for AGREE Single Layer execution
// - AGREE has been started
//
// Return:
// bool if valid, false otherwise
// ----------------------------------------------------------------------------
proc "is-contract-guarantees-valid" {
    wait-for-agree
    and [get-view "AGREE Results" | get-tree | get-item "Contract Guarantees" | get-property "columns[1]" -raw | contains "Valid"]
        [get-view "AGREE Results" | get-tree | get-item "Contract Guarantees" | get-property "columns[1]" -raw | contains "Invalid" | not]
}


// ----------------------------------------------------------------------------
// Checks that the AGREE Contract Consistency is valid.
//
// Desription:
// Checks the AGREE Results view and determines if the top-level Contract
// Consistency reports valid. Will wait for AGREE to finish before checking.
// 
// Assumptions:
// - Only works for AGREE Single Layer execution
// - AGREE has been started
//
// Return:
// bool if valid, false otherwise
// ----------------------------------------------------------------------------
proc "is-contract-consistency-valid" {
    wait-for-agree
    and [get-view "AGREE Results" | get-tree | get-item "Contract Consistency" | get-property "columns[1]" -raw | contains "Valid"]
        [get-view "AGREE Results" | get-tree | get-item "Contract Consistency" | get-property "columns[1]" -raw | contains "Invalid" | not]
}


// ----------------------------------------------------------------------------
// Asserts that the AGREE Contract Guarantees are valid.
//
// Desription:
// Checks the AGREE Results view and determines if the top-level Contract
// Guarantees reports valid. Will wait for AGREE to finish before checking.
//
// This will work for AGREE Single Layer and All Layers executions.
// 
// Assumptions:
// - AGREE has been started
// ----------------------------------------------------------------------------
proc "verify-contract-guarantees" [val condition[bool true]] {
    wait-for-agree

    get-view "AGREE Results" | get-tree | get-items | foreach [val item] {
        if [$item | get-text | contains "Contract Guarantees"] {
            if $condition {
                and [$item | get-property "columns[1]" -raw | contains "Valid"]
                    [$item | get-property "columns[1]" -raw | contains "Invalid" | not]
                    | verify-true
            } -else {
                and [$item | get-property "columns[1]" -raw | contains "Valid"]
                    [$item | get-property "columns[1]" -raw | contains "Invalid" | not]
                    | verify-false
            }
        }
    }
}


// ----------------------------------------------------------------------------
// Asserts that the AGREE Contract Consistency is valid.
//
// Desription:
// Checks the AGREE Results view and determines if the top-level Contract
// Consistency reports valid. Will wait for AGREE to finish before checking.
//
// This will work for AGREE Single Layer and All Layers executions.
// 
// Assumptions:
// - AGREE has been started
// ----------------------------------------------------------------------------
proc "verify-contract-consistency" [val condition[bool true]] {
    wait-for-agree
    get-view "AGREE Results" | get-tree | get-items | foreach [val item] {
        if[$item | get-text | contains "Contract Consistency"] {
            if $condition {
                and [$item | get-property "columns[1]" -raw | contains "Valid"]
                    [$item | get-property "columns[1]" -raw | contains "Invalid" | not]
                    | verify-true
            } -else {
                and [$item | get-property "columns[1]" -raw | contains "Valid"]
                    [$item | get-property "columns[1]" -raw | contains "Invalid" | not]
                    | verify-false
            }
        }
    }
}

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--
